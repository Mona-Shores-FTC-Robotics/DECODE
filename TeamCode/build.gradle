// build.gradle in TeamCode
//
// Most of the definitions for building your module reside in a common, shared
// file 'build.common.gradle'. Being factored in this way makes it easier to
// integrate updates to the FTC into your code. If you really need to customize
// the build definitions, you can place those customizations in this file, but
// please think carefully as to whether such customizations are really necessary
// before doing so.

apply from: '../build.common.gradle'
apply from: '../build.dependencies.gradle'

android {
    namespace = 'org.firstinspires.ftc.teamcode'

    packagingOptions {
        jniLibs {
            pickFirsts += ['**/*.so']
        }
        jniLibs.useLegacyPackaging true
    }

    lint {
        abortOnError true
        enable 'FieldName', 'LocalVariableName', 'MethodName', 'ClassName', 'ConstantNaming'
    }
}

repositories {
    maven { url = 'https://maven.brott.dev/' }
    maven { url = 'https://maven.pedropathing.com' }
    maven { url = "https://mymaven.bylazar.com/releases" }
    maven { url = "https://repo.dairy.foundation/releases"}

}

dependencies {
    implementation project(':FtcRobotController')

    //NextFTC
    implementation 'dev.nextftc:ftc:1.0.1'
    implementation 'dev.nextftc:hardware:1.0.1' // If you would like to use the hardware module
    implementation 'dev.nextftc:bindings:1.0.1'
    implementation 'dev.nextftc.extensions:pedro:1.0.0'

    // Pedro Pathing
    implementation('com.pedropathing:ftc:2.0.4')
    implementation('com.pedropathing:telemetry:1.0.0')

    //Panels
    implementation('com.bylazar:fullpanels:1.0.10')
    implementation "com.bylazar:configurables:1.0.4"


    //FTC Dashboard
    implementation('com.acmerobotics.dashboard:dashboard:0.5.1') {
        exclude group: 'org.firstinspires.ftc'
    }

    // AdvantageScope Lite - enables AdvantageScope connection via FTC Dashboard packets
//    implementation("page.j5155.AdvantageScope:lite:v26.0.0")

}

/**
 * Task to export autonomous paths as .pp files for Pedro Pathing visualizer.
 * Usage: ./gradlew :TeamCode:exportPaths
 * Optional: ./gradlew :TeamCode:exportPaths -PoutputDir=/path/to/output
 */
tasks.register('exportPaths', JavaExec) {
    group = 'FTC Utilities'
    description = 'Exports autonomous paths as .pp files for Pedro Pathing visualizer'

    // Depend on compilation
    dependsOn 'compileDebugJavaWithJavac'

    // Set up classpath for Android project
    classpath = files(
        android.sourceSets.main.java.srcDirs,
        "${buildDir}/intermediates/javac/debug/classes",
        configurations.runtimeClasspath
    )

    mainClass = 'org.firstinspires.ftc.teamcode.util.ExportAutoPaths'

    // Allow output directory to be specified via project property
    if (project.hasProperty('outputDir')) {
        args project.property('outputDir')
    } else {
        // Default to project root directory
        args projectDir.parent
    }
}
